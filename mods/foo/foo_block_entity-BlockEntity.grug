# Spawn the diamond 2 blocks above this block
pos: BlockPos = get_block_pos_above_n(get_block_pos_of_block_entity(me), 2)

# The dimension, so overworld/nether/end
level: Level = get_block_entity_level(me)

# Gets the full name, so "minecraft:diamond"
resource_location: ResourceLocation = resource_location("diamond")

# Create a diamond item
item: Item = item(resource_location)

# The coordinates where the diamond is spawned
center: Vec3 = get_block_pos_center(pos)
x: f32 = get_vec3_x(center)
y: f32 = get_vec3_y(center)
z: f32 = get_vec3_z(center)

# How often on_tick() has been called
tick: i32 = 0

on_tick() {
    # Spawn a diamond every 10 game ticks (0.5 seconds)
    tick = tick + 1
    if tick != 10 {
        return
    }
    tick = 0

    # Create a diamond item stack
    item_stack: ItemStack = item_stack(item)

    # Create a diamond item entity
    item_entity: ItemEntity = item_entity(level, x, y, z, item_stack)

    # Remove the random velocity that spawned items are given
    set_entity_delta_movement(item_entity_to_entity(item_entity), vec3_zero())

    # Spawn the diamond
    spawn_entity(item_entity_to_entity(item_entity), level)
}
